to setup ;run by observer -> called from dashboard
  reset-model
  load-data
  initialize-neighborhoods
  initialize-neighborhoods-data
  import-citizens
  create-migrants
  reset-ticks
end

to reset-model ;run by observer -> to setup
               ;instead of using clear-all, this method prevents the deletion of the GIS map, which is process intensive to load back in. 
  clear-turtles
  clear-globals
  clear-ticks
  clear-all-plots
  clear-output
  set y 2020
  set q 1 
end

to go ;run by observer -> called from dashboard
  if ticks >= 40 [ print "Ten years have passed, stopping the model.." stop ]
  consider-moving
  ;other stuff
  time-tick
end

to time-tick
  if q mod 4 = 0 [ set y y + 1 set q 0 update-neighborhoods create-migrants]
  set q q + 1
  tick
end  

to consider-moving ;run by observer -> to go
  ask citizens[
    let move-chance item (position "percent" item 0 movementage-data) item ( age + 1 ) movementage-data
    if random-float 1 < 2.5 * move-chance [ move-houses ]
    if current-neighborhood = 0 [ move-houses ] ;people that have not found a home yet, will always keep on looking.
  ]
end

to move-neighbhorhoods-buy [nh] ;run by citizen -> to move-houses
  if nh = current-neighborhood [ stop ] ;if citizen tries to move to current neighborhood, we assume no move is necessary.
  if is-agent? current-neighborhood [ ;add check if there is a current-neighborhood, because migrants do not have one (yet).
    ask current-neighborhood [set available_buy_houses available_buy_houses + 1]]
  move-to nh
  set current-neighborhood nh
  ask current-neighborhood [set available_buy_houses available_buy_houses - 1]
  if [available_buy_houses] of current-neighborhood < 1 [ set available-neighborhoods-buy neighborhoods with [ available_buy_houses > 0 ] ] ;rerun the availability if a neighborhood has no more houses available.
  set move-counter move-counter + 1
end

to move-neighbhorhoods-social-rent [nh] ;run by citizen -> to move-houses
  if nh = current-neighborhood [ stop ] ;if citizen tries to move to current neighborhood, we assume no move is necessary.
  if is-agent? current-neighborhood [ ;add check if there is a current-neighborhood, because migrants do not have one (yet).
    ask current-neighborhood [set available_social_rent_houses available_social_rent_houses + 1]]
  move-to nh
  set current-neighborhood nh
  ask current-neighborhood [set available_social_rent_houses available_social_rent_houses - 1]
  if [available_social_rent_houses] of current-neighborhood < 1 [ set available-neighborhoods-social-rent neighborhoods with [ available_social_rent_houses > 0 ] ] ;rerun the availability if a neighborhood has no more houses available.
  set move-counter move-counter + 1
end

to move-neighbhorhoods-part-rent [nh] ;run by citizen -> to move-houses
  if nh = current-neighborhood [ stop ] ;if citizen tries to move to current neighborhood, we assume no move is necessary.
  if is-agent? current-neighborhood [ ;add check if there is a current-neighborhood, because migrants do not have one (yet).
    ask current-neighborhood [set available_part_rent_houses available_part_rent_houses + 1]]
  move-to nh
  set current-neighborhood nh
  ask current-neighborhood [set available_part_rent_houses available_part_rent_houses - 1]
  if [available_part_rent_houses] of current-neighborhood < 1 [ set available-neighborhoods-part-rent neighborhoods with [ available_part_rent_houses > 0 ] ] ;rerun the availability if a neighborhood has no more houses available.
  set move-counter move-counter + 1
end

to move-houses ;run by citizen -> to consider-moving
  if available-neighborhoods-buy = nobody and available-neighborhoods-social-rent = nobody and available-neighborhoods-part-rent = nobody [ error "no availbility!" stop]
  let affordable-buy-neighborhoods available-neighborhoods-buy with [avg_price <= [budget] of myself * 9.35]
  let affordable-part-rent-neighborhoods available-neighborhoods-part-rent with [avg_price * 0.9 <= [budget] of myself * 9.35] ;assume renting is more afforable
  let affordable-social-rent-neighborhoods available-neighborhoods-social-rent with [avg_price * 0.75 <= [budget] of myself * 9.35] ;assume social renting is even more afforable
  if social-class = "low" [ set affordable-buy-neighborhoods nobody ] ;This is ugly, I know. But it works to make sure social class looks at right housing type.
  if social-class = "upper" or social-class = "middle" [ set affordable-social-rent-neighborhoods nobody ]
  
  ifelse affordable-buy-neighborhoods = nobody and affordable-social-rent-neighborhoods = nobody and affordable-part-rent-neighborhoods = nobody [ show "I am too poor to move" stop]
  [
    if affordable-part-rent-neighborhoods != nobody [ ask affordable-part-rent-neighborhoods [ define-utility myself "part-rent" ] ]
    if affordable-social-rent-neighborhoods != nobody [ ask affordable-social-rent-neighborhoods [ define-utility myself "social-rent" ] ]
    if affordable-buy-neighborhoods != nobody [ ask affordable-buy-neighborhoods [ define-utility myself "buy" ] ]
    
    let best-social-rent-option max-one-of social-rent-links [ utility ]
    let best-part-rent-option max-one-of part-rent-links [ utility ]
    let best-buy-option max-one-of buy-links [ utility ]
    
    let winner max-one-of ( link-set best-social-rent-option best-part-rent-option best-buy-option ) [utility]
    if is-buy-link? winner [ move-neighbhorhoods-buy other-side best-buy-option ]
    if is-part-rent-link? winner [ move-neighbhorhoods-part-rent other-side best-part-rent-option ]
    if is-social-rent-link? winner [ move-neighbhorhoods-social-rent other-side best-social-rent-option ]
    if winner = nobody [ show "I didn't find a best location!" stop ] 
  ]
  ;Remove links between citizen and neighborhoods so the next citizen can start calculating.
  ask social-rent-links [die]
  ask part-rent-links [die]
  ask buy-links [die]
end

to create-migrants
  let influx-count item ( position "tot_eth" item 0 migration-data) item (y - 2019) migration-data
  ;define ethnicity locals
  let p_dutch item ( position "dutch" item 0 migration-data) item (y - 2019) migration-data / influx-count
  let p_other_western ( item (position "other_western" item 0 migration-data) item (y - 2019) migration-data / influx-count ) + p_dutch
  let p_antilles ( item (position "antilles" item 0 migration-data) item (y - 2019) migration-data / influx-count ) + p_other_western
  let p_morocco ( item (position "morocco" item 0 migration-data) item (y - 2019) migration-data / influx-count ) + p_antilles
  let p_indonesian ( item (position "indonesian" item 0 migration-data) item (y - 2019) migration-data / influx-count ) + p_morocco
  let p_suriname ( item (position "suriname" item 0 migration-data) item (y - 2019) migration-data / influx-count ) + p_indonesian
  let p_turkey ( item (position "turkey" item 0 migration-data) item (y - 2019) migration-data / influx-count ) + p_suriname
  let p_eastern_eu ( item (position "eastern_eu" item 0 migration-data) item (y - 2019) migration-data / influx-count ) + p_turkey
  let p_other_nonwestern ( item (position "other_nonwestern" item 0 migration-data) item (y - 2019) migration-data / influx-count ) + p_eastern_eu
  ;define age locals
  let tot_age item (position "tot_age" item 0 migration-data) item (y - 2019) migration-data
  let p_young item (position "young" item 0 migration-data) item (y - 2019) migration-data / tot_age
  let p_middle ( item (position "middle" item 0 migration-data) item (y - 2019) migration-data / tot_age ) + p_young
  let p_old ( item (position "old" item 0 migration-data) item (y - 2019) migration-data / tot_age ) + p_middle
  ;taking averages of education around the city as input for migrants. THIS IS QUITE A BIG ASSUMPTION
  
  create-citizens influx-count / 2 / aggregate-cluster-size [ ;assume household size of 2
    setxy 367 (-239) set shape "person"
    set size 8
    set migrant? true
    ;lifephase
    let dice_lifephase random-float 1
    set lifephase (ifelse-value
      dice_lifephase <= p_young [ "young" ]
      dice_lifephase <= p_middle [ "middle" ]
      dice_lifephase <= p_old [ "old" ]
      [ "error" ])
    ;age
    set age (ifelse-value
      lifephase = "young" [ random 20 ]
      lifephase = "middle" [ random 45 + 20 ]
      lifephase = "old" [ random 35 + 65 ]
      [ 999 ])
    ;ethnicity
    let dice_ethnicity random-float 1
    set ethnicity (ifelse-value
      dice_ethnicity <= p_dutch ["dutch"]
      dice_ethnicity <= p_other_western [ "other_western" ]
      dice_ethnicity <= p_antilles [ "antilles" ]
      dice_ethnicity <= p_morocco [ "morocco" ]
      dice_ethnicity <= p_indonesian [ "indonesian" ]
      dice_ethnicity <= p_suriname [ "suriname" ]
      dice_ethnicity <= p_turkey [ "turkey" ]
      dice_ethnicity <= p_eastern_eu [ "eastern_european" ]
      dice_ethnicity <= p_other_nonwestern [ "other_nonwestern" ]
      [ "error" ])
    ;income/budget
    set budget round random-normal avg_migrant_income ( avg_migrant_income / 8 ) ;assuming std of 1/8 the average.
    set income (ifelse-value
      budget <= 25000 [ "low" ]
      budget <= 40000 [ "middle" ]
      budget <= 100000 [ "high" ]
      [ "error" ])
    ;education
    let dice_education random-float 1
    set education (ifelse-value
      dice_education <= p_edu_low [ "low" ]
      dice_education <= p_edu_middle [ "middle" ]
      dice_education <= p_edu_high [ "high" ]
      [ "error" ])
    if education = "high" and income = "low" [ if random-float 1 < 0.5 [ set education "low" ] ]
    if education = "low" and income = "high" [ if random-float 1 < 0.5 [ set education "high" ] ]
    calculate-social-class
    move-houses
  ]
end